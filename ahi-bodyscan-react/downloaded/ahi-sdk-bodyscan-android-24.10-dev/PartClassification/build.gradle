plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'jacoco'
}
def gradleToolsDir = "$project.projectDir/../ahi-sdk-build-tools-android/gradle-scripts"
apply from: "$gradleToolsDir/utilities.gradle"

def usingLocalCommonRepo() {
    return localPropertyDefined("ahi_sdk_common_android_local_dir")
}

def getLocalCommonRepo() {
    return gradle.ext.ahi_sdk_common_android_local_dir
}

android {
    compileSdk 33
    def SDK_VERSION_CODE = "cat VERSION.txt".execute().text.trim()
    defaultConfig {
        minSdk 26
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cppFlags "-frtti -fexceptions"
                abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
                arguments "-DOpenCV_DIR=" + "${projectDir}/../OpenCV-android-sdk" + "/sdk/native"
            }
            if (usingLocalCommonRepo()) {
                cmake {
                    arguments "-DANDROID_STL=c++_shared", "-DCPP_UTILS_PATH=${buildDir}/jniLibs", "-DLOCAL_COMMON_DIR=${getLocalCommonRepo()}"
                }
            } else {
                cmake {
                    arguments "-DANDROID_STL=c++_shared", "-DCPP_UTILS_PATH=${buildDir}/jniLibs"
                }
            }
        }
        buildConfigField 'String', 'SDK_VERSION_CODE', "\"${SDK_VERSION_CODE}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    if (!usingLocalCommonRepo()) {
        buildFeatures.prefab = true
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.18.1'
        }
    }
    packagingOptions {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
        jniLibs {
            excludes += ['**/libopencv_java4.so']
            excludes += ['**/libc++_shared.so', '**/libahi_common_util.so']
        }
    }
}

/**
 * Configuration of code coverage for unit tests
 */
tasks.withType(Test) {
    useJUnitPlatform()
    jacoco.includeNoLocationClasses = true
    jacoco.excludes = ['jdk.internal.*']
}

dependencies {
    api project(':Common')
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.0'
    implementation 'com.google.android.material:material:1.7.0'
    api "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    androidTestImplementation project(':PartResources')
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    def mockk_version = "1.12.2"
    testImplementation "io.mockk:mockk:$mockk_version"
    testImplementation "io.mockk:mockk-agent-jvm:$mockk_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
}